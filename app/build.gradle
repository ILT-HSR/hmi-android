apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.gitlab.arturbosch.detekt'
apply from: '../artifacts.gradle'

ext.versionMajor = 1
ext.versionMinor = 1
ext.versionPatch = 0

private Integer generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

private String generateVersionName() {
    return "${ext.versionMajor}.${ext.versionMinor}.${ext.versionPatch}"
}

android {
    compileSdkVersion 27
    buildToolsVersion '28.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "ch.hsr.ifs.gcs"
        minSdkVersion 26
        targetSdkVersion 27
        versionCode generateVersionCode()
        versionName generateVersionName()
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    testOptions{
        unitTests {
            includeAndroidResources = true
        }
    }
}

detekt {
    version = "$detektVersion"
    profile("main") {
        input = "$projectDir/app/src/main/kotlin"
        config = "$projectDir/detekt.yml"
    }
}

android {
    lintOptions {
        abortOnError false
    }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlinCoroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlinCoroutinesVersion"

    // Android
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:preference-v7:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:mediarouter-v7:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "android.arch.lifecycle:extensions:$lifecycleVersion"

    // External Libraries
    implementation "org.osmdroid:osmdroid-android:$osmDroidVersion"
    implementation "org.osmdroid:osmdroid-third-party:$osmDroidThirdPartyVersion"
    implementation "com.google.code.gson:gson:$gsonVersion"

    // Local Libraries
    implementation project(':jMAVlib')
    implementation project(':usbSerialForAndroid')

    // Testing
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.hamcrest:hamcrest-library:$hamcrestVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
}
